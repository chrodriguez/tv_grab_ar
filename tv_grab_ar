#!/usr/bin/perl -w

=pod

=head1 NAME

tv_grab_ar - Grab TV listings for Argentina.

=head1 SYNOPSIS

tv_grab_ar --help

tv_grab_ar --configure [--config-file FILE] [--gui OPTION]

tv_grab_ar [--config-file FILE] 
           [--days N] [--offset N]
           [--output FILE] [--quiet] [--debug]

tv_grab_ar --list-channels [--config-file FILE]
           [--output FILE] [--quiet] [--debug]
                 
                
=head1 DESCRIPTION

Output TV and listings in XMLTV format for many stations
available in Argentina. 

First you must run B<tv_grab_ar --configure> to choose which stations
you want to receive.

Then running B<tv_grab_ar> with no arguments will get a listings for
the stations you chose for five days including today.

=head1 OPTIONS

B<--configure> Prompt for which stations to download and write the
configuration file.

B<--config-file FILE> Set the name of the configuration file, the
default is B<~/.xmltv/tv_grab_ar.conf>.  This is the file written by
B<--configure> and read when grabbing.

B<--gui OPTION> Use this option to enable a graphical interface to be used.
OPTION may be 'Tk', or left blank for the best available choice.
Additional allowed values of OPTION are 'Term' for normal terminal output
(default) and 'TermNoProgressBar' to disable the use of Term::ProgressBar.

B<--output FILE> When grabbing, write output to FILE rather than
standard output.

B<--days N> When grabbing, grab N days rather than 5.

B<--offset N> Start grabbing at today + N days.  N may be negative.

B<--quiet> Suppress the progress-bar normally shown on standard error.

B<--debug> Provide more information on progress to stderr to help in
debugging.

B<--list-channels>    Output a list of all channels that data is available
                      for. The list is in xmltv-format.

B<--version> Show the version of the grabber.

B<--help> Print a help message and exit.

=head1 ERROR HANDLING

If the grabber fails to download data for some channel on a specific day, 
it will print an errormessage to STDERR and then continue with the other
channels and days. The grabber will exit with a status code of 1 to indicate 
that the data is incomplete. 

=head1 ENVIRONMENT VARIABLES

The environment variable HOME can be set to change where configuration
files are stored. All configuration is stored in $HOME/.xmltv/. On Windows,
it might be necessary to set HOME to a path without spaces in it.

=head1 SUPPORTED CHANNELS

For information on supported channels, see http://www.cablevisionfibertel.com.ar/

=head1 AUTHOR

Mattias Holmlund, mattias -at- holmlund -dot- se. This documentation
and parts of the code copied from tv_grab_uk by
Ed Avis, ed -at- membled -dot- com.

=head1 BUGS

=cut

use strict;

use XMLTV;
use XMLTV::ProgressBar;
use XMLTV::Options qw/ParseOptions/;
use XMLTV::Configure::Writer;

use XML::LibXML;
use Date::Manip;
use Compress::Zlib;
use File::Path;
use File::Basename;
use IO::Scalar;
use LWP;

my $ua;
$ua = LWP::UserAgent->new();
$ua->agent("xmltv/$XMLTV::VERSION");
$ua->env_proxy();

use HTTP::Cache::Transparent;

# Although we use HTTP::Cache::Transparent, this undocumented --cache
# option for debugging is still useful since it will _always_ use a
# cached copy of a page, without contacting the server at all.
#
use XMLTV::Memoize; XMLTV::Memoize::check_argv('getuncompressed');

sub t;

my $default_root_url = "https://buscador.cablevisionfibertel.com.ar/TVGridWS/TvGridWS.asmx";

my %services = ( 
  'provinces'     => 'GetProvinces?sitio=:site',
  'cities'        => 'GetLocalitys?sitio=:site&provincia=:province',
  'city'          => 'GetOneLocality?idLocalitySel=:city',
  'channels'      => 'GetAllSignalXML?clasDigId=:type_of_service&digHd=1&genre=',
  'channel_data'  => 'ReloadContentGridChannel?quantityDays=:days&idSignal=:channel_id'
);

my $default_cachedir = get_default_cachedir();

my( $opt, $conf ) = ParseOptions( { 
    grabber_name => "tv_grab_ar",
    capabilities => [qw/baseline manualconfig tkconfig apiconfig cache/],
    stage_sub => \&config_stage,
    listchannels_sub => \&list_channels,
    load_old_config_sub => \&load_old_config,
    version => '$Id: tv_grab_se_swedb.in,v 1.8 2010/10/01 17:49:30 dekarl Exp $',
    description => "Argentina (Cablevision)",

} );

if (not defined( $conf->{cachedir} )) {
    print STDERR "No cachedir defined in configfile " . 
                 $opt->{'config-file'} . "\n" .
                 "Please run the grabber with --configure.\n";
    exit 1;
}

if (not defined( $conf->{'root-url'} )) {
    print STDERR "No root-url defined in configfile " .
                 $opt->{'config-file'} . "\n" .
                 "Please run the grabber with --configure.\n";
    exit 1;
}

if (not defined( $conf->{'channel'} )) {
    print STDERR "No channels selected in configfile " .
                 $opt->{'config-file'} . "\n" .
                 "Please run the grabber with --configure.\n";
    exit 1;
}

init_cachedir( $conf->{cachedir}->[0] );
HTTP::Cache::Transparent::init( { 
    BasePath => $conf->{cachedir}->[0],
    NoUpdate => 15*60,
    Verbose => $opt->{debug},
    } );

binmode (STDOUT);

my($xmldecl, $channels) = load_channels( $conf );

my( $odoc, $root );
my $warnings = 0;

write_header( $xmldecl );

write_channel_list( $conf->{channel} );

my $bar = undef;
$bar = new XMLTV::ProgressBar( {
    name => 'downloading listings',
    count => 1 * @{$conf->{channel}},
    }) if (not $opt->{quiet}) && (not $opt->{debug});

foreach my $channel_id (@{$conf->{channel}})
{
    my $id = get_id_of_channel($channel_id);
    # We have already warned the user if the channel doesn't exist.
    if( exists $channels->{$id} )
    {
        my( $channel_name, $icon, $number ) = @{$channels->{$id}};
        t "  $channel_name";
        print_data( $conf, $id, $opt->{offset}, $opt->{days} )
            or warning( "Failed to download data for $channel_name");
    }
    $bar->update() if defined( $bar );
}

$bar->finish() if defined $bar;

write_footer();

# Signal that something went wrong if there were warnings.
exit(1) if $warnings;

# All data fetched ok.
t "Exiting without warnings.";
exit(0);

sub t
{
    my( $message ) = @_;
    print STDERR $message . "\n" if $opt->{debug};
}

sub warning
{
    my( $message ) = @_;
    print STDERR $message . "\n";
    $warnings++;
}

sub list_channels
{
    my( $conf, $opt ) = @_;

    ( $xmldecl, $channels ) = load_channels( $conf );
    
    my $result="";
    my $fh = new IO::Scalar \$result;
    my $oldfh = select( $fh );
    write_header( $xmldecl );
    write_channel_list( [sort keys %{$channels}] );
    write_footer();
    select( $oldfh );
    $fh->close();

    return $result;
}

sub config_stage
{
    my( $stage, $conf ) = @_;


    my $result;

    my $writer = new XMLTV::Configure::Writer( OUTPUT => \$result,
                                               encoding => 'iso-8859-1' );
    $writer->start( { grabber => 'tv_grab_ar' } );

    if ( $stage eq "start" )
    {
      #########################################################################
      # Select Base configuration parameters: cachedir, root-url & site
      #########################################################################

      $writer->write_string( {
          id => 'cachedir', 
          title => [ 
            [ 'Directory to store the cache in', 'en' ], 
            [ 'Directorio donde almacenar la cache', 'es' ] ],
          description => [ 
           [ 'tv_grab_ar uses a cache with files that it has already '. 
             'downloaded. Please specify where the cache shall be stored. ', 
             'en' ],
           [ 'tv_grab_ar utiliza una cache con archivos que ha descargado '. 
             'previamente. Por favor especifique el directorio donde almacenarlos. ',
             'es' ] ],
          default => $default_cachedir,
       } );

      $writer->write_string( {
          id => 'root-url', 
          title => [ 
            [ 'Root URL for grabbing data', 'en' ],
            [ 'URL base para obtener los datos', 'es' ] ],
          description => [ 
           [ 'At this URL you will find a description of provided services', 'en' ],
           [ 'En esta URL se provee info de los servicios de la API', 'es'] ],
          default => $default_root_url,
       } );

     $writer->write_string( {
         id => "site",
         title => [
           [ 'Site', 'en'],
           [ 'Sitio', 'es'] ],
         description => [
           [ 'Provider site parameter used in Web Services calls. CF works for '.
             'La Plata, Bs As. ', 'en' ],
           [ 'Parametro site usado por las llamadas a Web Services. CF funciona '.
             'para La Plata, Bs As. ', 'es' ] ],
         default => "CF"
       } );

     $writer->write_string( {
         id => "tz",
         title => [
           [ 'Timezone', 'en'],
           [ 'Zona horaria', 'es'] ],
         description => [
           [ 'Select your timezone ', 'en' ],
           [ 'Seleccione su zona horaria', 'es' ] ],
         default => "-0300"
       } );

      $writer->end( 'select-province' );

    }elsif( $stage eq 'select-province' )
    {
      #########################################################################
      # Select Province
      #########################################################################

      $writer->start_selectone( {
          id => 'province',
          title => [ 
            [ 'Province', 'en' ],
            [ 'Provincia', 'es' ] ],
          description => [ [ 'Select your provider Province.', 'en' ],
            [ 'Seleccione la provincia del proveedor', 'es' ] ],
        } );


      foreach my $province ( provinces( $conf )) {
        $writer->write_option( {
            value => $province,
            text  => [
              [ $province, 'en' ],
              [ $province, 'es' ]]
          } );
      }

      $writer->end_selectone();

      $writer->end( 'select-city' );

    }elsif( $stage eq 'select-city' )
    {
      #########################################################################
      # Select City
      #########################################################################

      $writer->start_selectone( {
          id => 'city',
          title => [ 
            [ 'City', 'en' ],
            [ 'Ciudad', 'es' ] ],
          description => [ [ 'Select your provider City.', 'en' ],
            [ 'Seleccione la ciudad del proveedor', 'es' ] ],
        } );


      foreach my $city ( cities( $conf )) {
        $writer->write_option( {
            value => $city->{id},
            text  => [
              [ $city->{name}, 'en' ],
              [ $city->{name}, 'es' ]]
          } );
      }

      $writer->end_selectone();

      $writer->end('select-type-of-service');

    }elsif( $stage eq 'select-type-of-service' )
    {

      #########################################################################
      # Select Type of service
      #########################################################################

      $writer->start_selectone( {
          id => 'type_of_service',
          title => [ 
            [ 'Type of service', 'en' ],
            [ 'Tipo de Servicio', 'es' ] ],
          description => [ [ 'Select your service.', 'en' ],
            [ 'Seleccione el servicio contratado', 'es' ] ],
        } );
      foreach my $type_of_service ( types_of_service( $conf )) {
        $writer->write_option( {
            value => $type_of_service->{id},
            text  => $type_of_service->{name}
          } );
      }

      $writer->end_selectone();

      $writer->end( 'select-channels' );


    }else {

      die "Unknown stage $stage"; 

    }
    return $result;
}

#
# Load a configuration file in the old format.
#

sub load_old_config
{
    my( $config_file ) = @_;

    my @lines = XMLTV::Config_file::read_lines( $config_file );
    
    my $conf = {};
    $conf->{cachedir}->[0] = $default_cachedir;
    $conf->{'root-url'}->[0] = $default_root_url;
    $conf->{channel} = [];

    foreach my $line (@lines)
    {
        next unless defined $line;

        my( $command, $param ) = split( /\s+/, $line, 2 );
        $param =~ tr/\n\r//d;
        $param =~ s/\s+$//;

        if ( $command =~ /^\s*root-url\s*$/) {
            $conf->{'root-url'}->[0] = $param;
        } elsif  ( $command =~ /^\s*channel\s*$/) {
            push @{$conf->{channel}}, $param;
        } elsif ( $command eq 'cache-dir' ) {
            $conf->{'cachedir'}->[0] = $param;
        } else {
            die "Unknown command $command in config-file $config_file"
        }
    }

    return $conf;
}

sub get_default_cachedir
{
    my $winhome = $ENV{HOMEDRIVE} . $ENV{HOMEPATH} 
    if defined( $ENV{HOMEDRIVE} ) 
        and defined( $ENV{HOMEPATH} ); 
    
    my $home = $ENV{HOME} || $winhome || ".";
    return "$home/.xmltv/cache";
}

sub init_cachedir
{
    my( $path ) = @_;
    if( not -d $path )
    {
        mkpath( $path ) or die "Failed to create cache-directory $path: $@";
    }
}

sub call_service_url
{
    my( $service, $query, $conf, $options ) = @_;

    my $url = service_url( $service, $conf, $options );

    my $xmldata = getuncompressed( $url );

    defined( $xmldata ) or die "Failed to fetch $url";

    my $xml = XML::LibXML::XPathContext->new( );

    $xml->registerNs( 'x', 'http://tempuri.org/');

    my $doc = XML::LibXML->new->parse_string($xmldata);

    return $xml->findnodes( $query, $doc );
}

sub provinces
{
    my( $conf ) = @_;

    my @provinces;

    foreach my $node (call_service_url('provinces', '//x:Provincia', $conf))
    {
          push(@provinces, $node->textContent);
    }

    return @provinces;
}

sub cities
{
    my( $conf ) = @_;

    my @cities;

    foreach my $node (call_service_url('cities', '//x:Technology', $conf))
    {
      my $id = $node->getChildrenByTagName('Id')->[0]->textContent;
      my $city = $node->getChildrenByTagName('Localidad')->[0]->textContent;
      push( @cities, { id => $id, name => $city });
    }

    return @cities;
}

sub types_of_service
{
    my( $conf ) = @_;

    my @types;

    my $city_description = call_service_url('city', '//x:Technology', $conf);

    my $classic = $city_description->[0]->getChildrenByTagName('ClasicoId');
    my $digital = $city_description->[0]->getChildrenByTagName('DigitalId');

    push( @types, {id => $classic, name => [['Classic', 'en'],['Clasico','es']] } ) if defined($classic);

    push( @types, {id => $digital, name => [['Digital', 'en'],['Digital','es']] } ) if defined($digital);

    return @types;
}

sub service_url
{
  my( $id, $conf, $options) = @_;

  my $url = $conf->{'root-url'}[0].'/'.$services{$id};

  if( $id eq 'provinces' )
  {
    $url =~ s/:site/$conf->{'site'}[0]/g;
  }
  elsif( $id eq 'cities' )
  {
    $url =~ s/:site/$conf->{'site'}[0]/g;
    $url =~ s/:province/$conf->{'province'}[0]/g;
  }
  elsif( $id eq 'city' )
  {
    $url =~ s/:city/$conf->{'city'}[0]/g;
  }
  elsif( $id eq 'channels' )
  {
    $url =~ s/:type_of_service/$conf->{'type_of_service'}[0]/g;
  }
  elsif( $id eq 'channel_data' )
  {
    $url =~ s/:days/$options->{days}/g;
    $url =~ s/:channel_id/$options->{channel_id}/g;
  }
  else{
    die "Service $id is unknown";
  }

  return $url;
}

sub load_channels
{
    my( $conf ) = @_;

    my %channels;

    foreach my $node (call_service_url('channels', '//x:Signal', $conf))
    {
      my $id = $node->getChildrenByTagName('Id')->[0]->textContent;
      if ($id > 0)
      {
        my $name    = $node->getChildrenByTagName('Name')->[0]->textContent;
        my $icon    = $node->getChildrenByTagName('LogoImage')->[0]->textContent;
        my $number  = $node->getChildrenByTagName('NumberSignal')->[0]->textContent;

        $channels{$id} = [ $name, $icon, $number ]
      }
    }

    return ("<?xml version='1.0' encoding='ISO-8859-1'?>\n", \%channels);
}

sub print_data
{
    my( $conf, $channel_id, $offset, $days ) = @_;

    my $now  = ParseDate( 'now' );
    my $date = $now;

    $date = DateCalc( $now, "+$offset days" ) if ($offset > 0);

    $date = Date_SetTime($date,0,0,0);

    foreach my $day_node (call_service_url('channel_data', '/x:string', $conf, {channel_id => $channel_id, days => $offset + $days})) 
    {
      my $doc = XML::LibXML->new->load_html( string => $day_node->textContent, recover => 2, suppress_errors => 1, suppress_warnings => 1);

      my $xml = XML::LibXML::XPathContext->new( $doc );

      foreach my $node ($xml->findnodes('//div[contains(@id,"contenedorProgramas")]/div[contains(@class,"columnChannel")]'))
      {
        next if ($offset-- > 0);

        foreach my $entry ($node->findnodes('.//div[contains(@class,"programaChannel")]'))
        {
          my $title = $entry->findvalue('./span[contains(@class,"titleChannel")]');
          my $season = $entry->findvalue('./span[contains(@class,"titleSeason")]');
          $season =~ s/^\s*\w:\s+//i;
          my $episode = $entry->findvalue('./span[contains(@class,"titleChapter")]');
          $episode =~ s/^\s*\w:\s+//i;

          next if $title eq '';

          my $top = $entry->getAttribute('style');

          next unless $top =~ /top:\s+(\d+)px/;

          $top=~ s/.*top:\s+(\d+)px.*/$1/;

          my $mins = $top * 0.5;

          my $start_at = UnixDate(DateCalc($date,"$mins minutes"), "%q ".$conf->{tz}->[0]);

          print "  <programme start='$start_at' channel='".channel_id($channel_id)."'>\n"; 
          print "    <title>".normalize_string($title)."</title>\n";
          if ($episode ne '' and $season ne '')
          {
            print "    <sub-title>".normalize_string($season)." | ".normalize_string($episode)."</sub-title>\n";
            $season =~ s/^[^\d]*(\d+).*$/$1/g;
            $season = --$season >= 0 ? $season : 0;
            $episode =~ s/^[^\d]*(\d+).*$/$1/g;
            $episode = --$episode >= 0 ? $episode : 0;
            print "    <episode-num system='xmltv_ns'>$season.$episode.0</episode-num>\n";
          }
          print "  </programme>\n";
          print "\n";
        }
        $date    = DateCalc( $date, "+1 day" ); 
      }

    }

    return 1;
}

sub write_header
{
    my( $xmldecl ) = @_;

    # Use the same xml declaration as the one in
    # channels.xml
    print $xmldecl;
    print '<!DOCTYPE tv SYSTEM "xmltv.dtd">' . "\n";
    print "<tv>\n";
}

sub normalize_string
{
  my( $string ) = @_;
  $string =~ s/&/&amp;/sg;
  return $string;
}

sub channel_id
{
  my( $id ) = @_;
  return "cablevision.$id";
}

sub get_id_of_channel
{
  my( $channel_id ) = @_;
  $channel_id =~ s/^cablevision\.//;
  return $channel_id;
}

sub write_channel_list
{
    my( $channel_list ) = @_;

    # Write list of channels.
    t 'Writing list of channels.';

    foreach my $channel_id (@{$channel_list})
    {
        my $id = get_id_of_channel($channel_id);

        if( not exists $channels->{$id} )
        {
            print STDERR "Unknown channel $channel_id." .
                " See http://www.cablevisionfibertel.com.ar/" . 
                " for a list of available channels or run" . 
                " tv_grab_ar --configure to reconfigure.\n";
            next;
        }

        my( $channel_name, $icon, $number ) = @{$channels->{$id}};

        $channel_name = normalize_string($channel_name);
        print "<channel id='".channel_id($id)."'>\n".
          "  <display-name>". $channel_name  ."</display-name>\n".
          "  <display-name>". $number  ."</display-name>\n".
          "  <icon src='".$icon."'/>\n".
          "</channel>\n";
    }
}

sub write_footer
{
    print "</tv>\n";
}

sub getuncompressed {
    my( $url ) = @_;

    my $response = $ua->get($url);

    return undef
        unless $response->is_success;

    my $compressed = $response->content
        or return undef;

    # Since LWP 5.827, the result from get() is already
    # uncompressed.

    my $uncompressed;

    eval {
      $uncompressed = Compress::Zlib::memGunzip( \$compressed );
    };

    $uncompressed = $compressed if not defined $uncompressed;

    return $uncompressed;
}

### Setup indentation in Emacs
## Local Variables:
## perl-indent-level: 4
## perl-continued-statement-offset: 4
## perl-continued-brace-offset: 0
## perl-brace-offset: -4
## perl-brace-imaginary-offset: 0
## perl-label-offset: -2
## cperl-indent-level: 4
## cperl-brace-offset: 0
## cperl-continued-brace-offset: 0
## cperl-label-offset: -2
## cperl-extra-newline-before-brace: t
## cperl-merge-trailing-else: nil
## cperl-continued-statement-offset: 2
## End:
